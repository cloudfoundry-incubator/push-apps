buildscript {
    ext {
        kotlinVersion = '1.2.0'
        junitPlatformVersion = '1.0.1'
        springBootVersion = '1.5.9.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply from: "$rootProject.rootDir/gradle/version.gradle"

allprojects {
    apply plugin: 'java'
    apply from: "$rootProject.rootDir/gradle/version.gradle"
    apply from: "$rootProject.rootDir/gradle/idea.gradle"

    repositories {
        jcenter()
        mavenCentral()
    }

    group = 'org.cloudfoundry.tools'
    version = "$pushAppsVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

task verifyLastpassLogin {
    doLast{
        def lsProcess = ['lpass', 'ls'].execute()
        lsProcess.waitForOrKill(5000)
        def exitValue = lsProcess.exitValue()

        if (exitValue != 0) {
            throw new GradleException('Must be logged in to lastpass')
        }
    }
}

static String getSecretsFromLastpass() {
    return ['lpass', 'show', 'Shared-CF-Denver-Pivotal/push-apps-ci'].execute().text
}

task setCiPipeline(dependsOn: 'verifyLastpassLogin') {
    doLast {
        def secrets = getSecretsFromLastpass()
        def secretsFile = File.createTempFile("secrets", ".tmp")
        secretsFile.write secrets
         exec {
             workingDir = "$rootProject.rootDir/ci"
             commandLine = [
                     'fly', '-tsuperpipe', 'sp', '-n', '-ppush-apps', '-cpush-apps.yml', '-l', "${secretsFile.absolutePath}"
             ]
         }
    }
}

task version {
    println "$pushAppsVersion"
}

task bumpPatch(type: UpdatePatchVersionTask) {
    versionFilePath = "$rootProject.rootDir/gradle/version.gradle"
}

class UpdatePatchVersionTask extends DefaultTask {
    @Input
    String versionFilePath

    @TaskAction
    def updatePatchVersion() {
        def versionFile = new File(versionFilePath)
        def currentVersion = versionFile.text
        def versionStringParts = currentVersion.split("\\.")
        def patchInt = Integer.parseInt(versionStringParts.last().replace("'\n", ""))
        versionStringParts[versionStringParts.size() - 1] = (patchInt + 1).toString() + "'\n"

        versionFile.text = versionStringParts.join(".")
    }
}

//noinspection GroovyAssignabilityCheck
task publishUploadedBintrayArtifacts {
    dependsOn(":components:push-apps:publishUploadedLibraryArchives", ":applications:cli:publishUploadedStandaloneArchives")
}
//noinspection GroovyAssignabilityCheck
task signBintrayArtifacts {
    dependsOn(":components:push-apps:signLibraryArtifacts", ":applications:cli:signStandaloneArtifacts")
}
//noinspection GroovyAssignabilityCheck
task syncArtifactsToMavenCentral {
    dependsOn(":components:push-apps:syncLibraryToMavenCentral", ":applications:cli:syncStandaloneToMavenCentral")
}

static void checkEnv(String env) {
    def envVar = System.getenv(env)

    if (envVar == null) {
        throw new GradleException("Must set $env env variable")
    }
}
